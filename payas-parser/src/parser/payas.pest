WHITESPACE = _{ " " | "," | "\t" | "\u{feff}" | line_terminator }
COMMENT = _{ "#" ~ (!line_terminator ~ ANY)* }
line_terminator = @{ "\r\n" | "\r" | "\n" }

system_document   = { SOI ~ model_definition+ ~ EOI }
model_definition = { "model" ~ name ~ table? ~ fields_definition }
fields_definition = { "{" ~ field_definition+ ~ "}" }
field_definition = { "public"? ~ field_name ~ ":" ~ field_type ~ pk? ~ column?}
field_type = { field_base_type ~ field_type_optional?}
field_base_type = {field_array_type | type_name}
field_type_optional = { "?" }
field_array_type = { "[" ~ type_name ~ "]" }

table = { "@table(\"" ~ name ~ "\")"}
column = { "@column(\"" ~ name ~ "\")"}
pk = { "@pk" }

field_name = { name }
type_name = { name}

name = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHA | ASCII_DIGIT | "_")* }
